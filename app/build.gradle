apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
	def rootExt = rootProject.ext

	compileSdkVersion rootExt.compileSdk
	buildToolsVersion rootExt.buildToolsVersion

	defaultConfig {
		minSdkVersion rootExt.minSdk
		targetSdkVersion rootExt.targetSdk

		versionCode generateVersionCode()
		versionName generateVersionName()

		applicationId rootExt.applicationId
		testInstrumentationRunner rootExt.testRunner
	}

	buildTypes {
		debug {
			resValue 'string', 'appName', 'Gitez dev'
			applicationIdSuffix '.debug'
			versionNameSuffix '.debug'

			minifyEnabled false
			useProguard false
			shrinkResources false
		}

		release {
			resValue 'string', 'appName', 'Gitez'

			minifyEnabled false
			useProguard false
			shrinkResources false

			files { rootProject.file('settings/proguard').listFiles() }.each {
				proguardFile it
			}
		}
	}

	sourceSets {
		androidTest.java.srcDirs += 'src/androidTest/kotlin'
		main.java.srcDirs += 'src/main/kotlin'
		test.java.srcDirs += 'src/test/kotlin'
	}
}

androidExtensions {
	experimental = true
	defaultCacheImplementation = 'SPARSE_ARRAY'
}

dependencies {
	def rootExt = rootProject.ext
	def appDependencies = rootExt.mainApplication
	def unitTestDependencies = rootExt.unitTesting
	def acceptanceTestDependencies = rootExt.acceptanceTesting

	// Base
	implementation fileTree(dir: 'libs', include: ['*.jar'])
	implementation project(':base')
	implementation project(':tim')
	implementation project(':util')
	implementation appDependencies.kotlin

	// Android
	implementation appDependencies.appCompat
	implementation appDependencies.coreKtx
	implementation appDependencies.constraintLayout

	// Unit/Integration tests
	testImplementation unitTestDependencies.kotlin
	testImplementation unitTestDependencies.kotlinTest
	testImplementation unitTestDependencies.junit

	// Acceptance tests
	androidTestImplementation acceptanceTestDependencies.testRunner
}
